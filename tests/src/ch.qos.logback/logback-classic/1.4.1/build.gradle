/*
 * Copyright and related rights waived via CC0
 *
 * You should have received a copy of the CC0 legalcode along with this
 * work. If not, see <http://creativecommons.org/publicdomain/zero/1.0/>.
 */
plugins {
	id "org.graalvm.internal.tck"
}

String libraryVersion = tck.testedLibraryVersion.get()

dependencies {
	testImplementation "ch.qos.logback:logback-classic:${libraryVersion}"
	testImplementation 'org.assertj:assertj-core:3.22.0'
}

task updateGeneratedMetadata {
	doLast {
		final ant = new groovy.ant.AntBuilder()
		final metadataDir = project.projectDir.toString() + "/build/native/agent-output/test"
		// replace typeReachable value for entries generated by LogbackTests test class
		ant.replace(file: metadataDir + "/reflect-config.json",
				token: "org.graalvm.logback.LogbackTests",
				value: "ch.qos.logback.core.util.OptionHelper")
	}
}

graalvmNative {
	binaries {
		test {
			buildArgs.add('--no-fallback')
		}
	}
	agent {
		defaultMode = "conditional"
		modes {
			conditional {
				userCodeFilterPath = "user-code-filter.json"
			}
		}
		accessFilterFiles.from("access-filter.json")
	}
}
