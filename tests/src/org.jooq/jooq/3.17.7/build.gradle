/*
 * Copyright and related rights waived via CC0
 *
 * You should have received a copy of the CC0 legalcode along with this
 * work. If not, see <http://creativecommons.org/publicdomain/zero/1.0/>.
 */
plugins {
    id "org.graalvm.internal.tck"
}

String libraryVersion = tck.testedLibraryVersion.get()

dependencies {
    testImplementation "org.jooq:jooq:$libraryVersion"
    testImplementation 'org.assertj:assertj-core:3.22.0'
    testImplementation 'com.h2database:h2:2.1.214'
    testImplementation 'ch.qos.logback:logback-classic:1.4.5'
}

task updateGeneratedMetadata {
    doLast {
        final ant = new groovy.ant.AntBuilder()
        final metadataDir = project.projectDir.toString() + "/build/native/agent-output/test"
        // replace typeReachable value for entries generated by JooqTest test class
        ant.replace(file: metadataDir + "/reflect-config.json",
                token: "org_jooq.jooq.JooqTest",
                value: "org.jooq.impl.SQLDataType")
    }
}

graalvmNative {
    agent {
        defaultMode = "conditional"
        modes {
            conditional {
                userCodeFilterPath = "metadata-user-code-filter.json"
            }
        }
        accessFilterFiles.from("access-filter.json")
    }
}
