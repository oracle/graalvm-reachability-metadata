/*
 * Copyright and related rights waived via CC0
 *
 * You should have received a copy of the CC0 legalcode along with this
 * work. If not, see <http://creativecommons.org/publicdomain/zero/1.0/>.
 */

plugins {
    id "org.graalvm.internal.tck"
}

String libraryVersion = tck.testedLibraryVersion.get()

dependencies {
    testImplementation "org.hibernate:hibernate-core:$libraryVersion"
    testImplementation 'org.assertj:assertj-core:3.22.0'
    testImplementation 'com.h2database:h2:2.1.214'
    testImplementation 'javax.validation:validation-api:2.0.1.Final'
    testImplementation 'ch.qos.logback:logback-classic:1.4.5'
    // Added for Hibernate 6 (Jakarta EE 9) API
    testImplementation 'jakarta.persistence:jakarta.persistence-api:3.0.0'
    // Ensure Logger type in AdditionalMetadataTest is available
    testImplementation 'org.jboss.logging:jboss-logging:3.4.3.Final'
    // Needed by PostgreSQLDialect initialization (uses org.postgresql.util.PGobject)
    testImplementation 'org.postgresql:postgresql:42.3.8'
}

task updateGeneratedMetadata {
    doLast {
        final ant = new groovy.ant.AntBuilder()
        final metadataDir = project.projectDir.toString() + "/generated-metadata"

        // replace typeReachable value for entries generated by AdditionalMetadataTest test class
        ant.replace(file: metadataDir + "/reflect-config.json",
                token: "org_hibernate.hibernate_core.AdditionalMetadataTest",
                value: "org.hibernate.jpa.HibernatePersistenceProvider")

        // replace typeReachable value for entries generated by IdentifierGeneratorTest test class
        ant.replace(file: metadataDir + "/reflect-config.json",
                token: "org_hibernate.hibernate_core.IdentifierGeneratorTest",
                value: "org.hibernate.id.factory.internal.DefaultIdentifierGeneratorFactory")
    }
}

task deleteGeneratedMetadata(type: Delete) {
    delete files(project.projectDir.toString() + "/generated-metadata")
}

graalvmNative {
    agent {
        defaultMode = "conditional"
        modes {
            conditional {
                userCodeFilterPath = "metadata-conditions-filter.json"
            }
        }
        metadataCopy {
            inputTaskNames.add("test")
            outputDirectories.add("generated-metadata")
            mergeWithExisting = true
        }
    }
    binaries {
        all {
            buildArgs.add('--report-unsupported-elements-at-runtime')
        }
    }
}
