/*
 * Copyright and related rights waived via CC0
 *
 * You should have received a copy of the CC0 legalcode along with this
 * work. If not, see <http://creativecommons.org/publicdomain/zero/1.0/>.
 */

plugins {
    id "org.graalvm.internal.tck"
}

String libraryVersion = tck.testedLibraryVersion.get()

dependencies {
    testImplementation "org.hibernate:hibernate-core:$libraryVersion"
    testImplementation 'org.assertj:assertj-core:3.22.0'
    testImplementation 'com.h2database:h2:2.1.214'
    testImplementation 'javax.validation:validation-api:2.0.1.Final'
    testImplementation 'ch.qos.logback:logback-classic:1.4.5'
}

task updateGeneratedMetadata {
    doLast {
        // replace typeReachable value for entries generated by AdditionalMetadataTest test class
        final ant = new groovy.ant.AntBuilder()
        final metadataDir = project.projectDir.toString() + "/generated-metadata"
        final testClass = "\"typeReachable\":\"org_hibernate.hibernate_core.AdditionalMetadataTest\""
        final hibernateClass = "\"typeReachable\":\"org.hibernate.jpa.HibernatePersistenceProvider\""
        ant.replace(file: metadataDir + "/reflect-config.json", token: testClass, value: hibernateClass)
    }
}

task deleteGeneratedMetadata(type: Delete) {
    delete files(project.projectDir.toString() + "/generated-metadata")
}

graalvmNative {
    agent {
        defaultMode = "conditional"
        modes {
            conditional {
                userCodeFilterPath = "metadata-conditions-filter.json"
                extraFilterPath = "metadata-extra-filter.json"
            }
        }
        metadataCopy {
            inputTaskNames.add("test")
            outputDirectories.add("generated-metadata")
            mergeWithExisting = true
        }
    }
}
