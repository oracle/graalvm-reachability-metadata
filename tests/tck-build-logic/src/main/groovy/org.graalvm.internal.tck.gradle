/*
 * Copyright and related rights waived via CC0
 *
 * You should have received a copy of the CC0 legalcode along with this
 * work. If not, see <http://creativecommons.org/publicdomain/zero/1.0/>.
 */

import org.graalvm.internal.tck.TestUtils

import static org.graalvm.internal.tck.TestUtils.metadataRoot
import static org.graalvm.internal.tck.TestUtils.repoRoot

plugins {
    id 'checkstyle'
    id 'java-library'
    id 'org.graalvm.buildtools.native'
}

repositories {
    mavenCentral()
}

TestUtils.locateRepoDirs(project) // Find relevant directories for the test harness.

checkstyle {
    configFile repoRoot.resolve("gradle").resolve("checkstyle.xml").toFile()
}

task checkstyle() {
    setDescription("Runs all checkstyle tasks")
    setGroup(JavaBasePlugin.VERIFICATION_GROUP)
    dependsOn tasks.withType(Checkstyle)
}

def metadataPaths = Objects.requireNonNullElse(
        System.getenv("GVM_TCK_MD").split(":"),
        providers.gradleProperty('metadata.dirs').get().split(":")
)

List<File> metadataFiles = []
for (def metadataPath : metadataPaths) {
    if (!metadataPath.startsWith("/")) {
        metadataFiles.add(metadataRoot.resolve(metadataPath).toFile())
    } else {
        metadataFiles.add(file(metadataPath))
    }
}

String libraryVersion = Objects.requireNonNullElse(
        System.getenv("GVM_TCK_LV"),
        providers.gradleProperty('library.version').get()
)

TestUtils.testedLibraryVersion = libraryVersion
// This value can be used to request specific library version to test with.

dependencies {
    testImplementation(platform(libs.test.junit.bom))
    testImplementation(libs.test.junit.jupiter.core)
    testImplementation(libs.test.junit.vintage)
}

tasks.named('test') {
    useJUnitPlatform()
}

graalvmNative {
    binaries {
        test {
            metadataFiles.forEach(metadataFile -> {
                configurationFileDirectories.from(metadataFile)
            })
            buildArgs.add('-H:+StrictConfiguration') // Necessary in order to test metadata properly
            verbose = true
        }
    }
}
